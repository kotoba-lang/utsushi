(ns langchain.parser
  "Output parsers — Runnables that turn assistant messages into data."
  (:require #?(:clj [clojure.edn :as edn]
               :cljs [cljs.reader :as edn])
            [clojure.string :as str]
            [langchain.message :as msg]
            [langchain.json :as json]))

(defn str-parser
  "Assistant message → plain text."
  []
  (fn [m] (msg/text m)))

(defn edn-parser
  "Assistant message → EDN. Strips ```edn fences if present."
  []
  (fn [m]
    (let [s (-> (msg/text m)
                (str/replace #"(?s)```(?:edn|clojure)?\s*(.*?)```" "$1")
                str/trim)]
      (edn/read-string s))))

(defn json-parser
  "Assistant message → data.
  - 0-arity: uses the in-language `kotoba-lang/json` parser (langchain.json).
  - 1-arity: uses a host-injected `json-read` fn (override for a faster/native
    parser; the original WASM-safe seam)."
  ([]
   (json-parser json/decode))
  ([json-read]
   (fn [m]
     (let [s (-> (msg/text m)
                 (str/replace #"(?s)```(?:json)?\s*(.*?)```" "$1")
                 str/trim)]
       (json-read s)))))
